plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    withJavadocJar()
    withSourcesJar()
}

project.version='1.0.0'
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.votoanthuan'
            artifactId = 'LightweightObjectPool'
            version = project.version
            from components.java

            pom {
                name = 'Lightweight Object Pool'
                description = 'Lightweight object pool based on a thread-local stack'
                url = 'https://github.com/votoanthuan/LightweightObjectPool'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ThuanVT'
                        name = 'Vo Toan Thuan'
                        email = 'votoanthuan@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/votoanthuan/LightweightObjectPool.git'
                    developerConnection = 'scm:git:ssh://github.com:votoanthuan/LightweightObjectPool.git'
                    url = 'https://github.com/votoanthuan/LightweightObjectPool'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            //Doc: https://central.sonatype.org/publish/publish-guide/#accessing-repositories
            //url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
//            url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
//            credentials {
//                username = ossrhUsername
//                password = ossrhPassword
//            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

jar{
    manifest{
        attributes 'Implementation-Version': project.version
        attributes 'Built-JDK': System.getProperty('java.version')
        attributes 'Build-Time': new Date().format("yyyy-MM-dd HH:mm:ss")
        attributes 'Website': 'https://github.com/votoanthuan/LightweightObjectPool'
    }
    //archiveFileName = "objectpool-${project.version}.jar"
}

